BIN_VERSION := env_var_or_default("BIN_VERSION", "")

adapter_name := "deno"
profile := env_var_or_default("profile", "debug")

os := env_var_or_default("os", os())
arch := \
if \
  env_var_or_default("arch", "") != "" { env_var_or_default("arch", "") } \
else if \
  arch() == "x86_64" { "amd64" } \
else if \
  arch() == "aarch64" { "arm64" } \
else \
  { arch() }

target := \
if \
  os + arch == "linuxamd64" { "x86_64-unknown-linux-gnu" } \
else if \
  os + arch == "linuxarm64" { "aarch64-unknown-linux-gnu" } \
else if \
  os + arch == "macosamd64" { "x86_64-apple-darwin" } \
else if\
  os + arch == "macosarm64" { "aarch64-apple-darwin" } \
else if \
  os + arch == "windowsamd64" { "x86_64-pc-windows-msvc" } \
else if \
  os + arch == "windowsarm64" { "aarch64-pc-windows-msvc" } \
else \
  { env_var_or_default("target", "debug") }

lib_extension := \
if \
  os == "windows" { "dll" } \
else if \
  os == "macos" { "dylib" } \
else if \
  os == "linux" { "so" } \
else if \
  os() == "windows" { "dll" } \
else if \
  os() == "macos" { "dylib" } \
else if \
  os() == "linux" { "so" } \
else \
  { "" }

profile_cargo := if profile != "debug" { "--profile " + profile } else { "" }

target_cargo := \
if \
target == "debug" \
  { "" } \
else if \
target == "" \
  { "" } \
else \
  { "--target " + target } 

root_dir := join(justfile_directory(), "..", "..")
out_dir :=  join(root_dir, "target", os + "-" + arch, profile)

build:
  mkdir -p {{out_dir}}/bin
  cargo build {{profile_cargo}} {{target_cargo}}
  cp \
    {{root_dir}}/target/.cargo/{{target}}/{{profile}}/libmach_adapter_{{adapter_name}}.{{lib_extension}} \
    {{out_dir}}/bin/lib{{adapter_name}}.{{lib_extension}}
