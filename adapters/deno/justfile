adapter_name := "deno"

BIN_VERSION := env_var_or_default("BIN_VERSION", "")
profile := env_var_or_default("profile", "debug")
os := env_var_or_default("os", "")
arch := env_var_or_default("arch", "")

target := \
if \
  os + arch == "linuxamd64" { "x86_64-unknown-linux-gnu" } \
else if \
  os + arch == "linuxarm64" { "aarch64-unknown-linux-gnu" } \
else if \
  os + arch == "macosamd64" { "x86_64-apple-darwin" } \
else if\
  os + arch == "macosarm64" { "aarch64-apple-darwin" } \
else if \
  os + arch == "windowsamd64" { "x86_64-pc-windows-msvc" } \
else if \
  os + arch == "windowsarm64" { "aarch64-pc-windows-msvc" } \
else \
  {""}

lib_extension := \
if \
  os == "windows" { "dll" } \
else if \
  os == "macos" { "dylib" } \
else if \
  os == "linux" { "so" } \
else if \
  os() == "windows" { "dll" } \
else if \
  os() == "macos" { "dylib" } \
else if \
  os() == "linux" { "so" } \
else \
  { "" }

build:
  just _create_out_dir
  cargo build \
    {{ if profile != "debug" { "--profile " + profile } else { "" } }} \
    {{ if target != "" { "--target " + target } else { "" } }}
  just _copy_cargo

_create_out_dir:
  mkdir -p ../../target/{{profile}}/adapters/{{adapter_name}}

_copy_cargo:
  cp ../../target/.cargo/{{profile}}/libmach_adapter_{{adapter_name}}.{{lib_extension}} ../../target/{{profile}}/adapters/{{adapter_name}}/lib.{{lib_extension}}
